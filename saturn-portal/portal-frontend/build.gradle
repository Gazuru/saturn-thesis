plugins {
    id "com.github.node-gradle.node" version "7.0.2"
    id 'java'
}
group = 'hu.bme.aut'
version = '0.0.1'

node {
    nodeProjectDir = file("${project.projectDir}")
}

tasks.register('npmRunBuild', NpmTask) {
    dependsOn tasks.npmInstall

    npmCommand.set(['run', 'build'])
    inputs.files('angular.json', '.browserslistrc', 'tsconfig.json', 'tsconfig.app.json', 'tsconfig.worker.json')
            .withPathSensitivity(PathSensitivity.RELATIVE)
    inputs.dir(project.fileTree('src').exclude('**/*.spec.ts'))
            .withPathSensitivity(PathSensitivity.RELATIVE)
    inputs.dir(project.fileTree('node_modules').exclude('.cache', '.cli-ngcc'))
    outputs.dir(project.layout.projectDirectory.dir('dist'))
    outputs.cacheIf { true }
}
